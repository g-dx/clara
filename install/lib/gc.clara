fn gc() {
    // TODO: Track object live counts & space freed
    debug("gc", "──────────────────────────────────────────────────────────────────────────── GC \n")
    gcMark()
    gcSweep()
    debug("gc", "───────────────────────────────────────────────────────────────────────────────\n")
}

// Tag-Free Garbage Collection for Strongly Typed Programming Languages
// https://cs.nyu.edu/goldberg/pubs/gold91.pdf
fn gcMark() {
    debug("gc", "🔎 Mark:\n")
    fp := getFramePointer()
    gc := fp.caller.gc
    while not fp.isStackBase() {
        fp = fp.next
        gc(fp)
        gc = fp.caller.gc
    }
}

// http://journal.stuffwithstuff.com/2013/12/08/babys-first-garbage-collector/
fn gcSweep() {
    debug("gc", "\n🔎️ Sweep:\n")
    b := getBlocks()
    prev := emptyBlock() // TODO: This is unfortunate! Block.None would be much nicer here!
    while b.isValidBlock() {
        if b.isMarked() {
            b.clearMark()
            prev = b
            b = b.next
        } else {
            unreached := b
            if prev.isValidBlock() {
                prev.next = unreached.next
            } else {
                setBlocks(unreached.next)
            }
            debug("gc", "  - (0x%lx) ✘\n", b)
            b = unreached.next
            clarafree(unreached)
        }
    }
}

fn noGc() nothing {
    // Called by the GC when tracing a GC root which is not in the heap (integers, bools, etc)
}

// TODO: this function _should_ be clearing bits in header!
fn clearMark(b: block) {
    if b.header == 1 {
        b.header = 0
    }
}

// TODO: This function _should_ be checking bit in header!
fn isMarked(b: block) bool {
    return b.header == 1 // GC marked it
}

// ---------------------------------------------------------------------------------------------------------------------
// External Functions
// ---------------------------------------------------------------------------------------------------------------------
fn isGcDebug() bool
fn isValidBlock(b: block) bool
fn emptyBlock() block