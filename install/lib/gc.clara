fn gc() {
    // TODO: Track object live counts & space freed
    debug("gc", "â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ GC \n")
    gcMark()
    gcSweep()
    debug("gc", "â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€\n")
}

// Tag-Free Garbage Collection for Strongly Typed Programming Languages
// https://cs.nyu.edu/goldberg/pubs/gold91.pdf
fn gcMark() {
    debug("gc", "ğŸ” Mark:\n\n")
    fp := getFramePointer()
    roots := fp.map.roots

    j := 0
    while not fp.isStackBase() {
        fp = fp.next
        debug("gc", "â–¶ (%d) <description>\n", j)
        i := 0
        while i < roots.length {
            gcMarkSlot(fp, roots[i])
            i = i + 1
        }
        roots = fp.map.roots
        j = j + 1
    }
}

fn gcMarkSlot(fp: frame, off: byte) {
    gcMarkPointer(unsafe(fp, -off * 8, pointer), 1)
}

fn gcMarkPointer(p: pointer, level: int) {
    p = unsafe(p.deref, 0, pointer)
    block := unsafe(p, -16, block)
    info := typeInfoTable()[block.typeId()]

    if block.isReadOnly() {
        gcMarkDebug(p, level, info, "ğŸ”")
        return
    }
    if block.isMarked() {
        gcMarkDebug(p, level, info, "ğŸ†—")
        return
    }

    // Mark block & process type
    gcMarkDebug(p, level, info, "âœ…")
    block.setMark()
    match info {
        case StructType(name, roots):
            i := 0
            while i < roots.length {
                gcMarkPointer(unsafe(p, roots[i] * 8, pointer), level + 5)
                i = i + 1
            }
        case StringType():
            // Already marked - no further action required
    }
}

fn gcMarkDebug(p: pointer, level: int, type: typeInfo, icon: string) {
    debug("gc", " - (0x%lx) %s%*s", p, icon, level, "")
    typeName := "<unknown>"
    match type {
        case StructType(name, roots):
            typeName = name
        case StringType():
            typeName = "string"
    }
    debug("gc", "'%s'\n", typeName)
}

// http://journal.stuffwithstuff.com/2013/12/08/babys-first-garbage-collector/
fn gcSweep() {
    debug("gc", "\n\nğŸ—‘ï¸ğŸ§¹ Sweep:\n")
    b := getBlocks()
    prev := emptyBlock() // TODO: This is unfortunate! Block.None would be much nicer here!
    while b.isValidBlock() {
        if b.isMarked() {
            b.clearMark()
            prev = b
            b = b.next
        } else {
            unreached := b
            if prev.isValidBlock() {
                prev.next = unreached.next
            } else {
                setBlocks(unreached.next)
            }
            debug("gc", "  - (0x%lx) ğŸ”¥\n", b.inc(16)) // Increment pointer to point to start of user mem
            b = unreached.next
            clarafree(unreached)
        }
    }
}

struct pointer {
    deref: int
}

fn isNull(p: pointer) bool = unsafe(p, 0, int) == 0

// Compile time generated structure holding runtime type information
enum typeInfo {
    StructType(name: string, roots: []byte)
    StringType()
}

fn setMark(b: block) {
    if b.isMarked() {
        panic("setMark() called on marked block!")
    }
    b.header = b.header | 0x1
}

fn clearMark(b: block) {
    if not b.isMarked() {
        panic("clearMark() called on unmarked block!")
    }
    b.header = b.header & 0x7FFFFFFFFFFFFFFE // TODO: ~0x1 would be nicer
}

fn isMarked(b: block) bool = (b.header & 0x1) == 0x1
fn isReadOnly(b: block) bool = (b.header & 0x2) == 0x2
fn typeId(b: block) int = (b.header & 0xFFFF << 48) >> 48

// ---------------------------------------------------------------------------------------------------------------------
// External Functions
// ---------------------------------------------------------------------------------------------------------------------
fn isValidBlock(b: block) bool
fn emptyBlock() block

// Implemented in codegen.go
fn typeInfoTable() []typeInfo