fn gc() {
    // TODO: Track object live counts & space freed
    debug("gc", "â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ GC \n")
    gcMark()
    gcSweep()
    debug("gc", "â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€\n")
}

// Tag-Free Garbage Collection for Strongly Typed Programming Languages
// https://cs.nyu.edu/goldberg/pubs/gold91.pdf
fn gcMark() {
    debug("gc", "ğŸ” Mark:\n\n")
    fp := getFramePointer()
    roots := fp.map.roots

    // -------------------------------------------------------------
    // Stack
    // -------------------------------------------------------------
    debug("gc", "Stack\n")
    j := 0
    while not fp.isStackBase() {
        fp = fp.next
        debug("gc", "â–¶ (%d) <description>\n", j)
        for root in roots {
            gcMarkSlot(fp, root)
        }
        roots = fp.map.roots
        j = j + 1
    }


    // -------------------------------------------------------------
    // Global variables
    // -------------------------------------------------------------
    debug("gc", "\nGlobals\n")
    gcMarkPointer(unsafe(getRuntime(), 0, type(int)), 0)
}

fn gcMarkSlot(fp: frame, off: byte) {
    gcMarkPointer(unsafe(fp, -off * 8, type(pointer)).deref, 1)
}

fn gcMarkPointer(ptr: int, level: int) {
    if ptr == 0 {
        gcMarkDebug(ptr, level, "???", "âŒ")
        return
    }
    block := unsafe(ptr, -16, type(block))
    info := typeInfoTable()[block.typeId()]

    if block.isReadOnly() {
        gcMarkDebug(ptr, level, info.getName(), "ğŸ”")
        return
    }
    if block.isMarked() {
        gcMarkDebug(ptr, level, info.getName(), "ğŸ†—")
        return
    }

    // Mark block & process type
    gcMarkDebug(ptr, level, info.getName(), "âœ…")
    block.setMark()
    match info {
        case StructType(name, roots):
            for root in roots {
                gcMarkPointer(deref(ptr, root * 8), level + 5)
            }
        case StringType():
            // Already marked - no further action required
        case ArrayType(name, elemIsPointer):
            if elemIsPointer {
                len := deref(ptr, 0)
                for i in 0 .. len {
                    gcMarkPointer(deref(ptr, 8 + (i * 8)), level + 5)
                }
            }
    }
}

fn gcMarkDebug(ptr: int, level: int, name: string, icon: string) {
    debug("gc", " - (0x%09lx) %s%*s", ptr, icon, level, "")
    debug("gc", "'%s'\n", name)
}

// http://journal.stuffwithstuff.com/2013/12/08/babys-first-garbage-collector/
fn gcSweep() {
    debug("gc", "\n\nğŸ—‘ï¸ğŸ§¹ Sweep:\n")
    b := getBlocks()
    prev := emptyBlock() // TODO: This is unfortunate! Block.None would be much nicer here!
    while b.isValidBlock() {
        if b.isMarked() {
            b.clearMark()
            prev = b
            b = b.next
        } else {
            unreached := b
            if prev.isValidBlock() {
                prev.next = unreached.next
            } else {
                setBlocks(unreached.next)
            }
            debug("gc", "  - (0x%lx) ğŸ”¥\n", b.inc(16)) // Increment pointer to point to start of user mem
            b = unreached.next
            clarafree(unreached)
        }
    }
}

struct pointer {
    deref: int
}

fn deref(ptr: int, off: int) int = unsafe(ptr, off, type(pointer)).deref

// Compile time generated structure holding runtime type information
enum typeInfo {
    StructType(name: string, roots: []byte)
    StringType()
    ArrayType(name: string, elemIsPointer: bool)
}

fn getName(t: typeInfo) string {
    match t {
        case StructType(name, roots):
            return name
        case ArrayType(name, elemIsPointer):
            return name
        case StringType():
            return "string"
    }
}

fn setMark(b: block) {
    if b.isMarked() {
        panic("setMark() called on marked block!")
    }
    b.header = b.header | 0x1
}

fn clearMark(b: block) {
    if not b.isMarked() {
        panic("clearMark() called on unmarked block!")
    }
    b.header = b.header & ~0x1
}

fn isMarked(b: block) bool = (b.header & 0x1) == 0x1
fn isReadOnly(b: block) bool = (b.header & 0x2) == 0x2
fn typeId(b: block) int = (b.header & 0xFFFF << 48) >> 48

// ---------------------------------------------------------------------------------------------------------------------
// External Functions
// ---------------------------------------------------------------------------------------------------------------------

// Implemented in codegen.go
fn typeInfoTable() []typeInfo