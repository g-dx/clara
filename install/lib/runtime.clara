// Linked List of all stack frames up to stack base
struct frame {
    next: frame
    map: gcMap
}

fn slot(f: frame, off: byte) pointer = unsafe(f, -off * 8, type(pointer))

// GC roots at a particular point in a stack frame
struct gcMap {
    roots: []byte
}

// Runtime information
struct runtime {
    args: []string
    env: map«string, string»
}

// C -> Clara entrypoint
#[ExtRet]
fn entrypoint(argc: int, argv: pointer, envp: pointer) {
    setStackBase(getFramePointer())

    // Configure runtime
    setRuntime(Runtime(parseArgs(argc, argv), parseEnv(envp)))

    main() // Off we go...
}

fn parseArgs(argc: int, argv: pointer) []string {
    args := stringArray(argc, "")
    buf := NewByteBuffer(16)
    for i in 0 .. argc {
        args[i] = buf.parseCString(argv.inc(i * 8).deref).toString()
        buf.reset()
    }
    return args
}

fn parseCString(buf: byteBuffer, arg: pointer) byteBuffer {
    off := 0
    while true {
        bytes := arg.inc(off).deref.toTaggedInt()
        b := bytes & 0xFF
        if b == 0x00 {
            return buf
        }
        buf.append(b)
        off = off + 1
    }
    return buf
}

fn parseEnv(envp: pointer) map«string, string» {
    env := NewHashMap«string, string»(fnv1a, Equals)
    buf := NewByteBuffer(16)

    // envp is a pointer to an array of pointers. Each entry points to a read-only
    // string which contains the env var in the form "NAME=VALUE". The end of the
    // array is marked by a null pointer
    idx := envp
    entry := idx.deref
    while not entry.isNull() {
        s := buf.parseCString(entry).toString()
        i := s.IndexOf("=") // TODO: Update IndexOf() to support passing a []byte
        if i == -1 {
            env.put(s, "")
        } else {
            // TODO: Should be calling buf.size() here
            env.put(buf.substring(0, i), buf.substring(i+1, buf.size-i-1))
        }
        buf.reset()
        idx = idx.inc(8) // Point to next array index
        entry = idx.deref  // Get value
    }
    return env
}


// Called from user code to crash the program
fn panic(cause: string) {
    printf("\n// -----------------------------------------------------------------------------\n")
    printf("// Panic: %s\n", cause)
    printf("// -----------------------------------------------------------------------------\n")
    exit(1)
}

fn assert(condition: bool, msg: string) {
    if not condition {
        panic(msg)
    }
}

// Invoked by an ASM trampoline (See codegen.go) for invalid array access
fn indexOutOfBounds(index: int, length: int) {
    printf("\n// -----------------------------------------------------------------------------\n")
    printf("// Panic: index out of bounds! index = %d, array.length = %d\n", index, length)
    printf("// -----------------------------------------------------------------------------\n")
    printf("\nBacktrace:\nTODO!\n\n")
    // TODO: Output stacktrace
    exit(1)
}

// ---------------------------------------------------------------------------------------------------------------------

// Runtime representation of a closure
struct closure {
    f: fn(clEnv, int, int, int, int, int) nothing
    env: clEnv
}

struct clEnv {
    // Captured variables ...
}

fn invokeDynamic(p: pointer, arg1: int, arg2: int, arg3: int, arg4: int, arg5: int) {
    if p.isClosure() {
        cl := unsafe(p, 0, type(closure))
        cl.f(cl.env, arg1, arg2, arg3, arg4, arg5)
    } else {
        f := unsafe(p, 0, type(fn(int, int, int, int, int) nothing))
        f(arg1, arg2, arg3, arg4, arg5)
    }
}

// readonly = true ➞ fn pointer, readonly = false ➞ closure
fn isClosure(p: pointer) bool {
    header := p.inc(-8).deref.toRawInt()
    return (header & 0x2) == 0x0
}

// ---------------------------------------------------------------------------------------------------------------------
// Runtime tagging support

fn tag(i: int) int = unsafe(i, 0, type(pointer)).toTaggedInt()
fn untag(i: int) int = unsafe(i, 0, type(pointer)).toUntaggedInt()

// ---------------------------------------------------------------------------------------------------------------------
// Raw memory access

fn toPointer«T»(t: T, off: int) pointer = unsafe(t, off, type(pointer))

// ---------------------------------------------------------------------------------------------------------------------
// External Functions
// ---------------------------------------------------------------------------------------------------------------------

 // Implemented in assembly by codegen.go & called by C main. Invokes entrypoint(int, int, int) defined
 // above. Defined here to prevent redeclares.
fn asm_entrypoint() int

// Source: libc, https://www.gnu.org/software/libc/manual/html_node/Normal-Termination.html#Normal-Termination
#[RawValues]
fn exit(status: int) nothing

fn getFramePointer() frame

fn isStackBase(f: frame) bool
fn setStackBase(f: frame) nothing
fn setRuntime(r: runtime) nothing
fn getRuntime() runtime

// Raw memory access (Implemented in assembly by codegen.go)
fn readInt(p: pointer, idx: int) int
fn readByte(p: pointer, idx: int) int
fn writeByte(p: pointer, idx: int, val: int) nothing
fn writeInt(p: pointer, idx: int, val: int) nothing

// WARNING: Type system escape hatch! Use with extreme caution!
//
// p: Pointer to heap
// off: Offset from pointer
// _type: Type of return type
// Returns the calculated memory location interpreted as the parameterised type
fn unsafe(p: pointer, off: int, _type: nothing) pointer // Implemented in assembly by codegen.go
